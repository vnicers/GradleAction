import com.vnicers.build.gradle.ProjectVersion

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

configure(rootProject) {
    description = "moa"

    project.version = new ProjectVersion(major.toInteger(), minor.toInteger(), release.toBoolean())


    task printVersion {
        group 'version'

        doLast {
            logger.quiet "Version: $version"
        }
    }

    gradle.taskGraph.whenReady { TaskExecutionGraph taskGraph ->
        if (taskGraph.hasTask(release)) {
            if (!version.release) {
                version.release = true

                ant.propertyfile(file: versionFile) {
                    entry(key: 'release', type: 'string', operation: '=', value: 'true')
                }
            }
        }
    }

    task wrapper(type: Wrapper) {
        description = "Generates gradlew[.bat] scripts"
        gradleVersion = '4.1'

        doLast() {
            def gradleOpts = "-XX:MaxMetaspaceSize=1024m -Xmx1024m"
            def gradleBatOpts = "$gradleOpts -XX:MaxHeapSize=256m"
            File wrapperFile = file("gradlew")
            wrapperFile.text = wrapperFile.text.replace("DEFAULT_JVM_OPTS=",
                    "GRADLE_OPTS=\"$gradleOpts \$GRADLE_OPTS\"\nDEFAULT_JVM_OPTS=")
            File wrapperBatFile = file("gradlew.bat")
            wrapperBatFile.text = wrapperBatFile.text.replace("set DEFAULT_JVM_OPTS=",
                    "set GRADLE_OPTS=$gradleBatOpts %GRADLE_OPTS%\nset DEFAULT_JVM_OPTS=")
        }
    }

    tasks.addRule("Pattern: increment<Classifier>Version - Increment the project version classifier.") { taskName ->
        if (taskName.startsWith('increment') && taskName.endsWith('Version')) {
            task(taskName) {
                group 'version'
                doLast {
                    String classifier = (taskName - 'increment' - 'Version').toLowerCase()
                    String currentVersion = version.toString()

                    switch (classifier) {
                        case 'major': ++version.major
                            break
                        case 'minor': ++version.minor
                            break
                        default:
                            throw new GradleException("Invalid version type '$classifier'. Allowed types :['Major','Minor']")
                    }

                    String newVersion = version.toString()

                    logger.quiet "Incrementing $classifier project version : $currentVersion ->$newVersion"

                    ant.propertyfile(file: versionFile) {
                        entry(key: classifier, type: 'int', operation: '+', value: 1)
                    }

                    logger.quiet "Current project version : $version"
                }
            }
        }

    }
}


subprojects {
    group = 'com.vnicers'

    version = rootProject.version

    apply plugin: 'java'
    apply plugin: 'idea'

    idea {
        module {
            outputDir file('build/classes/java/main')
            testOutputDir file('build/classes/java/test')
        }
    }


    configurations.all {
        // Check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    // ext.gradleScriptDir = "${rootProject.projectDir}/gradle"

    sourceSets {
        // Make the compileOnly dependencies available when compiling/running tests
        test.compileClasspath += configurations.compileOnly
        test.runtimeClasspath += configurations.compileOnly
    }

    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        options.encoding = 'UTF-8'
        options.compilerArgs += "-parameters"
    }

    test {
        testLogging {
            // Show that tests are run in the command-line output
            events 'STARTED', 'PASSED', 'SKIPPED', 'FAILED', 'STANDARD_OUT', 'STANDARD_ERROR'
        }

        scanForTestClasses = false
        include(["**/*Tests.class", "**/*Test.class"])
        // Since we set scanForTestClasses to false, we need to filter out inner
        // classes with the "$" pattern; otherwise, using -Dtest.single=MyTests to
        // run MyTests by itself will fail if MyTests contains any inner classes.
        exclude(["**/Abstract*.class", '**/*$*'])
        reports.junitXml.setDestination(file("$buildDir/test-results"))
    }


    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        maven { url "https://jitpack.io" }
    }

    dependencies {

        implementation 'org.apache.commons:commons-lang3:3.6'
        implementation 'ch.qos.logback:logback-classic:1.2.3'


        compileOnly 'org.projectlombok:lombok:1.16.18'

        testCompile 'junit:junit:4.12'
    }

}
